locals {
  ecr_repositories = toset([
    "gateway",
    "scraper-ticketmaster",
    "deduper",
    "enricher",
    "matcher",
    "notifier",
    "web",
    "postgres",
    "recommender",
  ])
}

# Images are tagged with the git sha only, so a tag never moves.
resource "aws_ecr_repository" "app" {
  for_each = local.ecr_repositories

  name                 = "concert-radar/${each.key}"
  image_tag_mutability = "IMMUTABLE"
  force_delete         = true

  image_scanning_configuration {
    scan_on_push = true
  }
}

resource "aws_ecr_lifecycle_policy" "app" {
  for_each = aws_ecr_repository.app

  repository = each.value.name
  policy = jsonencode({
    rules = [{
      rulePriority = 1
      description  = "Keep the last 10 images"
      selection = {
        tagStatus   = "any"
        countType   = "imageCountMoreThan"
        countNumber = 10
      }
      action = {
        type = "expire"
      }
    }]
  })
}
